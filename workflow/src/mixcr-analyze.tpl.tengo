//tengo:hash_override D70EDB25-6FF6-4615-966D-B79B04B5751C

self := import("@platforma-sdk/workflow-tengo:tpl")
smart := import("@platforma-sdk/workflow-tengo:smart")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")
ll := import("@platforma-sdk/workflow-tengo:ll")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
pcolumn := import("@platforma-sdk/workflow-tengo:pframes.pcolumn")
times := import("times")
text := import("text")
maps := import("@platforma-sdk/workflow-tengo:maps")

json := import("json")

self.defineOutputs("qc", "reports", "log", "clns")

mixcrSw := assets.importSoftware("@platforma-open/milaboratories.software-mixcr:memory-from-limits")

progressPrefix := "[==PROGRESS==]"

self.body(func(inputs) {
	inputData := inputs[pConstants.VALUE_FIELD_NAME]
	aggregationAxesNames := inputs[pConstants.AGGREGATION_AXES_NAMES_FIELD_NAME]

	preset := inputs.preset
	library := inputs.library

	params := inputs.params
	species := params.species
	limitInput := params.limitInput
	leftAlignmentMode := params.leftAlignmentMode
	rightAlignmentMode := params.rightAlignmentMode
	materialType := params.materialType
	tagPattern := params.tagPattern
	assembleClonesBy := params.assembleClonesBy
	perProcessMemGB := inputs.perProcessMemGB
	perProcessCPUs := inputs.perProcessCPUs
	fileExtension := params.fileExtension
	reports := params.reports
	isLibraryFileGzipped := params.isLibraryFileGzipped

	presetContent := inputs.presetContent.getDataAsJson()

	if !is_map(presetContent) || !is_array(presetContent.pipeline) {
		ll.panic("malformed presetContent %v", presetContent)
	}

	inputDataMeta := inputData.getDataAsJson()

	hasAssembleContigs := false
	hasAssembleCells := false
	hasMiToolSteps := false
	for stage in presetContent.pipeline {
		if stage == "assembleContigs" {
			hasAssembleContigs = true
		} else if stage == "assembleCells" {
			hasAssembleCells = true
		} else if text.has_prefix(stage, "mitool-") {
			hasMiToolSteps = true
		}
	}

	clnsFileName := "result.clns"
	if hasAssembleContigs {
		clnsFileName = "result.contigs.clns"
	}
	if hasAssembleCells {
		clnsFileName = "result.assembledCells.clns"
	}

	mixcrCmdBuilder := exec.builder().
		printErrStreamToStdout().
		saveStdoutStream().
		env("MI_USE_SYSTEM_CA", "true").
		env("MI_PROGRESS_PREFIX", progressPrefix).
		software(mixcrSw).
		secret("MI_LICENSE", "MI_LICENSE").
		arg("analyze").arg("--use-local-temp")

	// CPUs number per process
	if !is_undefined(perProcessCPUs) {
	    mixcrCmdBuilder.cpu(perProcessCPUs)
	} else {
	    mixcrCmdBuilder.cpu(16)     // default CPUs number per sample
	}

	// Memory limit per process
    if !is_undefined(perProcessMemGB) {
        // memory per process was set in "Advanced Settings" block directly
		mixcrCmdBuilder.mem(string(perProcessMemGB) + "GiB")
	} else if hasMiToolSteps {
		// allow to use more memory for single cell analysis with mitool pre-
		mixcrCmdBuilder.mem("192GiB")
	} else if hasAssembleContigs || hasAssembleCells {
		// allow to use more memory for single cell analysis
		mixcrCmdBuilder.mem("110GiB")
	} else {
		mixcrCmdBuilder.mem("64GiB")
	}

	if !is_undefined(limitInput) {
		mixcrCmdBuilder.arg("--limit-input").arg(string(limitInput))
	}

	if library {
		mixcrCmdBuilder.arg("--species").arg(species)
		if isLibraryFileGzipped {
			mixcrCmdBuilder.arg("--library").arg("library.json.gz").
			addFile("library.json.gz", library)
		} else {
			mixcrCmdBuilder.arg("--library").arg("library.json").
			addFile("library.json", library)
		}
	} else if !is_undefined(species) {
		species = params.species
		mixcrCmdBuilder.arg("--species").arg(species)
	}

	if smart.isResource(preset) /* file */ {
		mixcrCmdBuilder.
			arg("local#input_preset").
			addFile("input_preset.yaml", preset)
	} else {
		ll.assert(!is_undefined(preset.name), "undefined preset name")
		mixcrCmdBuilder.
			arg(preset.name)
	}

	// Optional preset-specific parameters (order matters if preset requires positional values)
	if !is_undefined(leftAlignmentMode) {
		mixcrCmdBuilder.arg(leftAlignmentMode)
	}
	if !is_undefined(rightAlignmentMode) {
		mixcrCmdBuilder.arg(rightAlignmentMode)
	}
	if !is_undefined(materialType) {
		mixcrCmdBuilder.arg(materialType)
	}
	if !is_undefined(tagPattern) {
		mixcrCmdBuilder.arg("--tag-pattern").arg(tagPattern)
	}
	if !is_undefined(assembleClonesBy) {
		mixcrCmdBuilder.arg("--assemble-clonotypes-by").arg(assembleClonesBy)
	}
	// Error correction mode
	errorCorrectionMode := params.errorCorrectionMode
	if errorCorrectionMode == "relaxed" {
		mixcrCmdBuilder.
			arg("-Massemble.cloneAssemblerParameters.minimalQuality=20").
			arg("-Massemble.cloneAssemblerParameters.badQualityThreshold=10").
			arg("-Massemble.cloneAssemblerParameters.cloneClusteringParameters.searchParameters=oneMismatchOrIndel").
			arg("-Massemble.cloneAssemblerParameters.cloneClusteringParameters.searchDepth=1")
	} else if errorCorrectionMode == "off" {
		mixcrCmdBuilder.arg("-Massemble.cloneAssemblerParameters.cloneClusteringParameters=null")
	}

	inputMap := inputData.inputs()
	if inputDataMeta.keyLength == 0 {
		ll.assert(aggregationAxesNames == [], "unexpected aggregation axes names")
		// Assuming only one key "[]" exists here, no need for sorted iteration
		inputFile := inputMap["[]"]
		ll.assert(!is_undefined(inputFile), "unexpected agg group structure")
		inputFileName := "input." + fileExtension
		mixcrCmdBuilder.addFile(inputFileName, inputFile)
		mixcrCmdBuilder.arg(inputFileName)
	} else if inputDataMeta.keyLength == 1 {
		ll.assert(aggregationAxesNames == ["pl7.app/sequencing/readIndex"], "unexpected aggregation axes names")
		for sKey in maps.getKeys(inputMap) {
			inputFile := inputMap[sKey]
			key := json.decode(sKey)
			if len(key) != 1 {
				ll.panic("malformed key: %v", sKey)
			}
			r := key[0]
			if (r[0] != 'R' && r[0] != "I") || (r[1] != '1' && r[1] != '2') || len(r) != 2 {
				ll.panic("malformed read index: %v", r)
			}
			mixcrCmdBuilder.addFile("input_" + r + "." + fileExtension, inputFile)
		}
		mixcrCmdBuilder.arg("input_{{R}}." + fileExtension)
	} else if inputDataMeta.keyLength == 2 {
		ll.assert(aggregationAxesNames == ["pl7.app/sequencing/lane", "pl7.app/sequencing/readIndex"], "unexpected aggregation axes names")
		for sKey in maps.getKeys(inputMap) {
			inputFile := inputMap[sKey]
			key := json.decode(sKey)
			if len(key) != 2 {
				ll.panic("malformed key: %v", sKey)
			}
			lane := key[0]
			r := key[1]
			if (r[0] != 'R' && r[0] != "I") || (r[1] != '1' && r[1] != '2') || len(r) != 2 {
				ll.panic("malformed read index: %v", r)
			}
			if is_undefined(int(lane)) {
				ll.panic("malformed lane: %v", lane)
			}
			mixcrCmdBuilder.addFile("input_L" + lane + "_" + r + "." + fileExtension, inputFile)
		}
		mixcrCmdBuilder.arg("input_L{{n}}_{{R}}." + fileExtension)
	} else {
		ll.panic("too many axes / not supported")
	}

	mixcrCmdBuilder.arg("result")
	mixcrCmdBuilder.arg("--force-overwrite")

	mixcrCmdBuilder.saveFile("result.qc.json")
	mixcrCmdBuilder.saveFile(clnsFileName)

	for report in reports {
		mixcrCmdBuilder.saveFile(report.fileJson)
		mixcrCmdBuilder.saveFile(report.fileTxt)
	}

	mixcrCmdBuilder = mixcrCmdBuilder.cache(48 * times.hour)
	mixcrCmd := mixcrCmdBuilder.run()

	// collecting results

	reportsMap := pcolumn.resourceMapBuilder( /* keyLength */ 2 )
	for report in reports {
		reportsMap.add([report.id, "json"], mixcrCmd.getFile(report.fileJson))
		reportsMap.add([report.id, "txt"], mixcrCmd.getFile(report.fileTxt))
	}

	result := {
		qc: mixcrCmd.getFile("result.qc.json"),
		log: mixcrCmd.getStdoutStream(),
		reports: reportsMap.build(),
		clns: mixcrCmd.getFile(clnsFileName)
	}

	return result
})
