// get preset

self := import("@platforma-sdk/workflow-tengo:tpl")
smart := import("@platforma-sdk/workflow-tengo:smart")
ll := import("@platforma-sdk/workflow-tengo:ll")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
mixcr := import(":mixcr")

self.validateInputs({
	"__options__,closed": "",
	"preset": "any",
	"params": {
		"__options__,closed": "",
		"species,omitempty": "string"
	}
})

mixcrSw := assets.importSoftware("@platforma-open/milaboratories.software-mixcr:main")

self.defineOutputs("preset", "presetSpecForBack")

self.body(func(inputs) {
	preset := inputs.preset
	species := inputs.params.species

	mixcrExportPresetCmdBuilder := exec.builder().
		inUiQueue().
		software(mixcrSw).
		secret("MI_LICENSE", "MI_LICENSE").
		printErrStreamToStdout().
		arg("exportPreset")

	if smart.isResource(preset) /* file */ {
		mixcrExportPresetCmdBuilder.
			arg("--preset-name").
			arg("local#input_preset").
			addFile("input_preset.yaml", preset)
	} else {
		ll.assert(!is_undefined(preset.name), "undefined preset name")
		mixcrExportPresetCmdBuilder.
			arg("--preset-name").
			arg(preset.name)
	}

	if !is_undefined(species) {
		mixcrExportPresetCmdBuilder.arg("--species").arg(species)
	}

	mixcr.addAdditionalColumnArgs(mixcrExportPresetCmdBuilder, {assemblingFeature: "CDR3", imputed: false})

	mixcrExportPresetCmd := mixcrExportPresetCmdBuilder.arg("preset.json").
		saveFileContent("preset.json").
		saveFile("preset.json").
		run()

	presetContent := mixcrExportPresetCmd.getFileContent("preset.json")

	mixcrForBackCmd := exec.builder().
		inUiQueue().
		software(mixcrSw).
		secret("MI_LICENSE", "MI_LICENSE").
		printErrStreamToStdout().
		arg("presetSpecificationsForBack").
		addFile("preset.yaml", mixcrExportPresetCmd.getFile("preset.json")).
		arg("preset.yaml").
		arg("presetForBack.json").
		saveFileContent("presetForBack.json").
		run()

	presetSpecForBackContent := mixcrForBackCmd.getFileContent("presetForBack.json")

	return {
		preset: presetContent,
		presetSpecForBack: presetSpecForBackContent
	}
})
