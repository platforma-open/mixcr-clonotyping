wf := import("@platforma-sdk/workflow-tengo:workflow")

render := import("@platforma-sdk/workflow-tengo:render")

maps := import("@platforma-sdk/workflow-tengo:maps")
ll := import("@platforma-sdk/workflow-tengo:ll")
assets := import("@platforma-sdk/workflow-tengo:assets")
smart := import("@platforma-sdk/workflow-tengo:smart")
file := import("@platforma-sdk/workflow-tengo:file")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
json := import("json")

calculatePresetInfoTpl := assets.importTemplate(":calculate-preset-info")
processTpl := assets.importTemplate(":process")

wf.setPreRun(assets.importTemplate(":prerun"))

wf.prepare(func(args){
	return {
		resolvedLibraryInput: wf.resolve(args.inputLibrary, { errIfMissing: false })
	}
})

wf.body(func(args) {
	// (begin) @TODO To be moved to SDK / Model

	fileImports := {}

	importFile := func(importHandle) {
		fromMap := fileImports[importHandle]
		if fromMap == undefined {
			fImport := file.importFile(importHandle)
			fileImports[importHandle] = fImport
			return fImport.file
		} else {
			return fromMap.file
		}
	}

	// (end)

	blockId := wf.blockId().getDataAsJson()

	inputRef := args.input

	preset := args.preset

	// file or json with preset name
	presetResource := undefined

	if preset.type == "file" {
		presetResource = importFile(preset.file)
	} else {
		ll.assert(preset.type == "name", "unexpected preset type")
		// whole json, including type
		presetResource = smart.createJsonResource(preset)
	}

	limitInput := args.limitInput
	isLibraryFileGzipped := false

	library := false
	libraryId := undefined
	species := args.species
	libraryImportHandle := undefined
	receptorsOrChains := ["IG", "TCRAB", "TCRGD"]
	if !is_undefined(args.chains) {
		receptorsOrChains = args.chains
	}

	if !is_undefined(args.resolvedLibraryInput) {
		library = args.resolvedLibraryInput.data
		libraryId = args.resolvedLibraryInput.spec["domain"]["pl7.app/vdj/libraryId"]
		librarySpec := args.resolvedLibraryInput.spec
		species = librarySpec["annotations"]["pl7.app/species"]
	} else if !is_undefined(args.libraryFile) {
		fImport := file.importFile(args.libraryFile)
		libraryImportHandle = fImport.handle
		library = fImport.file
		libraryId = blockId
		species = args.customSpecies
		isLibraryFileGzipped = args.isLibraryFileGzipped
	}

	input := wf.resolve(inputRef)

	//ll.print("__THE_LOG__ " +input)

	presetInfoResult := render.create(calculatePresetInfoTpl, {
		preset: presetResource,
		params: {
			species: species
		}
	})

	presetContent := presetInfoResult.output("preset", 24 * 60 * 60 * 1000)
	presetSpecForBack := presetInfoResult.output("presetSpecForBack", 24 * 60 * 60 * 1000)

	// calculating chains
	// chains := ["IGHeavy", "IGLight", "TCRAlpha", "TCRBeta", "TCRGamma", "TCRDelta"]
	//receptorsOrChains := ["IG", "TCRAB", "TCRGD"]

	runMixcr := render.createEphemeral(processTpl, {
		inputSpec: input.getFutureInputField("spec"),
		inputData: input.getFutureInputField("data"),

		preset: presetResource,
		presetSpecForBack: presetSpecForBack,
		presetContent: presetContent,

		library: library,

		params: smart.createJsonResource({
			species: species,
			receptorsOrChains: receptorsOrChains,
			limitInput: limitInput,
			blockId: blockId,
			libraryId: libraryId,
			presetCommonName: args.presetCommonName,
			isLibraryFileGzipped: isLibraryFileGzipped
		})
	})

	exports := {
		qc: {
			spec: runMixcr.output("qc.spec"),
			data: runMixcr.output("qc.data")
		},
		reports: {
			spec: runMixcr.output("reports.spec"),
			data: runMixcr.output("reports.data")
		},
		clonotypes: runMixcr.output("clonotypes"),
		clns: {
			spec: runMixcr.output("clns.spec"),
			data: runMixcr.output("clns.data")
		}
	}

	if !is_undefined(args.libraryFile) {
		// If library file is provided, we need to exported it for native mixcr blocks to use it (i.e. SHM trees)
		exports.library = {
			data: library,
			spec: {
				kind: "File",
				name: "pl7.app/vdj/library",
				domain: {
					"pl7.app/vdj/libraryId": libraryId // == blockId
				},
				annotations: {
					"pl7.app/species": species,
					"pl7.app/vdj/isLibrary": "true",
					"pl7.app/vdj/libraryFormat": (isLibraryFileGzipped ? "repseqio.json.gz" : "repseqio.json"),
					"pl7.app/label": "Library from MiXCR Clonotyping"
				}
			}
	 	}
	}

	outputs := {
		qc: pframes.exportColumnData(runMixcr.output("qc.data")),
		reports: pframes.exportColumnData(runMixcr.output("reports.data")),
		logs: runMixcr.output("logs.data"),
		clns: runMixcr.output("clns.data"),
		clonotypes: pframes.exportFrame(runMixcr.output("clonotypes")),
		fileImports: smart.createMapResource(maps.mapValues(fileImports, func(im) {
			return im.handle
		}))
	}

	if !is_undefined(libraryImportHandle) {
		outputs.libraryImportHandle = libraryImportHandle
	}

	return {
		outputs: outputs,
		exports: exports
	}
})
